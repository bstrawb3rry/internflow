plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.2'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'jvm-test-suite'
}

group = 'com.start.spring'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

sourceSets {
	intTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	intTestImplementation.extendsFrom implementation, testImplementation
	intTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.register('intTest', Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.intTest.output.classesDirs
	classpath = sourceSets.intTest.runtimeClasspath
	shouldRunAfter test

	useJUnitPlatform()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation('org.springframework.boot:spring-boot-starter-security')
	implementation 'org.springframework.security:spring-security-config:6.0.2'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.0.2'
	implementation 'org.postgresql:postgresql:42.5.4'
	intTestImplementation 'io.rest-assured:spring-mock-mvc:5.3.0'
	intTestImplementation 'com.h2database:h2:2.1.214'
}

tasks.named('test') {
	useJUnitPlatform()
}

bootRun.doFirst {
	bootRun.systemProperty('spring.profiles.active', 'dev')
}
